#!/usr/bin/perl

use strict;
use FindBin;
use lib "$ENV{GUS_HOME}/lib/perl";
use Getopt::Long;
use SchemaInstallUtils::Main::Utils qw( runSql dropSchemaSetTables dropSchemaSetPostgres getDbh );
use DBI;
use Cwd;

$| = 1;

my ($dbName, $dbHost, $dropApiDB, $dropGUS, $create, $allowFailures);

&GetOptions("dbName=s" => \$dbName,
	    "dbHost=s" => \$dbHost,
	    "dropApiDB!" => \$dropApiDB,
	    "dropGUS!" => \$dropGUS,
	    "allowFailures!" => \$allowFailures,
	    "create!" => \$create);

my $x = $dropApiDB + $dropGUS + $create;
if (!$dbName || !$dbHost || ($x != 1)) {
  die "
Install the ApiDB and ApiDBTuning schemas and the ApidDB patches to GUS, or uninstall them (or uninstall the GUS schemas)

Usage: installApidbSchema --dbName name --dbHost host [--create | --dropApiDB | --dropGUS] [--allowFailures]

Where:
  - dbName: database to connect to, eg, toxo-inc
  - dbHost: database server, eg, ares10.penn.apidb.org
  - create: creates the ApiDB and ApiDBTuning schemas and patches GUS.
  - dropApiDB: drops the ApiDB and ApiDBTuning schemas, and ApiDB patches to GUS.
  - dropGUS: drops the Core, DoTS, PROT, RAD, SRes, STUDY, TESS (and all VER) schemas
  - allowFailures: ignores SQL failures.

Caution: only use --allowFailures if you know what you are doing

Log is printed to STDERR

Include the following in your bash environment or on the command line preceding the command below:
export DB_PLATFORM=Oracle; export DB_USER=MY-LOGIN; export DB_PASS=MY-PASSWORD;

Example: ./installApidbSchema --dbName rm8711 --dbHost ares10.penn.apidb.org --create
";
}

# Run DDL as this role so new objects are owned by this role.
my $runAsRole = 'GUS_W';

my $login = $ENV{DB_USER};
my $password = $ENV{DB_PASS};
my $dbVendor = $ENV{DB_PLATFORM};
die "DB Platform must be either 'Oracle' or 'Postgres'\n" unless ($dbVendor eq 'Oracle' || $dbVendor eq 'Postgres');

my $dbh = getDbh($dbName, $dbHost, $dbVendor, $login, $password);

my $d = 'GUS schema';
$d = 'ApiDB and ApiDBTuning schemas' if $dropApiDB;

if ($dropApiDB || $dropGUS) {
    print "\nYou are about to UNINSTALL the $d on:\n  $dbName\nType the name of the instance to confirm: ";
    my $confirm = <STDIN>;
    chomp $confirm;
    die "You did not correctly confirm the db instance\n" unless $dbName eq $confirm;
}

# PLEASE READ BEFORE ADDING TO @create
#
# The list is ordered according to these rules:
# first:  changes to GUS
# second: create ApidbTuning schema
# third:  create Apidb schema
# fourth: create tables within Apidb, in dependency order as needed
#
# This ordering helps ensure that when/if we drop, the dropping proceeds
# in the correct order, avoiding bogus failures from dropping what was
# not yet created.
#
# ALSO: if you are adding a create script for the Apidb schema
#       it is good practice to write a parallel drop script
#       so that if needed, the create can be manually reversed

my @create = qw(
  removeNullConstraintsFromSimilarityTables.sql
  createGusTuning.sql
  createApidbSchema.sql
  createApidbTuningSchema.sql
  createWebreadySchema.sql
  createChebiSchema.sql
  createOrganism.sql
  createEdaSchema.sql
  createAnalysisMethodInvocation.sql
  createDatasource.sql
  createSnpTables.sql
  createGeneInteractionTables.sql
  createGeneFeatureProduct.sql
  createGeneFeatureName.sql
  createOrthologGroup.sql
  createECTables.sql
  createGroupMapping.sql
  createPhylogeneticProfile.sql
  createPhylogeneticTree.sql
  createRelatedNaFeature.sql
  createReportCache.sql
  createSequenceAttributeTables.sql
  createStoredProcedures.sql
  createPivotProcedure.sql
  createSyntenyTables.sql
  createSpliceSiteFeature.sql
  createPubChemSubstance.sql
  createPubChemCompound.sql
  createDbRefCompound.sql
  createOldAnnotation.sql
  createOldCodingSequence.sql
  createTuningManager.sql
  createWorkflow.sql
  createIntronJunction.sql
  createNAFeatureHaploblock.sql
  createBlatProteinAlignment.sql
  createGFF3Table.sql
  createEcNumberGenus.sql
  createSpliceSiteGenes.sql
  createPolyAGenes.sql
  createMassSpecSummary.sql
  createCompoundMassSpec.sql
  createPathwayTables.sql
  createTranscriptProduct.sql
  createIsolateGPS.sql
  createPhenotype.sql
  createNAFeaturePhenotype.sql
  createPhenotypeScore.sql
  createPhenotypeMutants.sql
  createPhenotypeGrowthRate.sql
  createHaplotypeResult.sql
  createOntologyTermResult.sql
  createSubjectResult.sql
  createSeqEdit.sql
  createWHOStandards.sql
  createLOPITResults.sql
  createWGCNAResults.sql
  createWGCNAModuleEigengeneResults.sql
  createNAFeatureMetaCycle.sql
  createCNVTables.sql
  createRflpTables.sql
  createNAFeatureImage.sql
  createSequenceTaxon.sql
  createFeatureLocation.sql
  createCrisprPhenotype.sql
  createGoSubset.sql
  createDatabaseTableMapping.sql
  createEntityGraphTables.sql
  createExternalResourceUrl.sql
  createAGPPiece.sql
  createAlphaFoldTable.sql
  createLegacyDatasetNames.sql
  createBusco.sql
  createPdb.sql
  createCellxgene.sql
  grantChebiTables.sql
  createEpitopesTable.sql
  createInterproResults.sql
  createGenomeBedGraph.sql
  createProteinBedGraph.sql
  createMsPeptide.sql
  createLongReadTranscript.sql
);

my @delete = qw(
  restoreNullConstraintsFromSimilarityTables.sql
  dropGusTuning.sql
  dropApidbSchemas.sql
);

my @gusSchemas = qw(
  CORE
  DOTS
  MODEL
  PLATFORM
  RESULTS
  SRES
  STUDY
);

my @apiDbSchemas = qw(
  APIDB
  APIDBTUNING
  CHEBI
  EDA
  WEBREADY
);

my $filesDir = "$ENV{GUS_HOME}/lib/sql/apidbschema/$dbVendor";

if ($create) {
  for my $sqlFile (@create) {
     print STDERR "\n==============================================================\n";
     print STDERR "$sqlFile\n";
     print STDERR "==============================================================\n";

     my $filePath = "$filesDir/$sqlFile";

     my @sqlplusParamValues;
     runSql($login, $password, $dbh, $dbVendor, $filePath, $allowFailures, $runAsRole, @sqlplusParamValues);

  }
} elsif ($dropApiDB) {
  if ($dbVendor eq 'Oracle') {
    my $schemaSetStr = join ', ', map "'$_'", @apiDbSchemas;
    dropSchemaSetTables($dbh, $schemaSetStr);  # drop only the tables, etc, not the users which are managed by DBA
  } elsif ($dbVendor eq 'Postgres') {
    dropSchemaSetPostgres($dbh, @apiDbSchemas);
  } else {
    die "Unsupported dbVendor:$dbVendor.";
  }
  for my $sqlFile (@delete) {
    print STDERR "\n==============================================================\n";
    print STDERR "$sqlFile\n";
    print STDERR "==============================================================\n";

    my $filePath = "$filesDir/$sqlFile";
    runSql($login, $password, $dbh, $dbVendor, $filePath, $allowFailures, $runAsRole);
  }
} elsif ($dropGUS) {
  if ($dbVendor eq 'Oracle') {
    my $schemaSetStr = join ', ', map "'$_'", @gusSchemas;
    dropSchemaSetOracle($dbh, $schemaSetStr);  # drop the users.  these will be recreated by the gus install program
  } elsif ($dbVendor eq 'Postgres') {
    dropSchemaSetPostgres($dbh, @gusSchemas);
  } else {
    die "Unsupported dbVendor:$dbVendor.";
  }
}

print STDERR "\nDone.\n";
